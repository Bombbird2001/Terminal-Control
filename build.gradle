buildscript {
    ext.kotlin_version = '1.8.21'

    repositories {
        mavenLocal()
        mavenCentral()
        //jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://repo.spring.io/plugins-release/" }
        maven { url "https://maven.scijava.org/content/repositories/public/" }
        google()
    }

    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.6'
        classpath 'com.android.tools.build:gradle:7.4.1'
        classpath 'com.mobidevelop.robovm:robovm-gradle-plugin:2.3.18'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: "idea"

    version = '1.5.2111.2'
    ext {
        appName = "Terminal Control"
        gdxVersion = '1.11.0'
        roboVMVersion = '2.3.18'
        versionCode = 82
    }

    repositories {
        google()
        mavenLocal()
        mavenCentral()
        //jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://repo.spring.io/plugins-release/" }
        maven { url "https://maven.scijava.org/content/repositories/public/" }
    }
}

project(":desktop") {
    apply plugin: "java"
    apply plugin: "kotlin"

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        implementation "club.minnced:java-discord-rpc:2.0.2"
        implementation "club.minnced:discord-rpc-release:v3.4.0"
        implementation "de.dfki.mary:marytts-runtime:5.2"
        implementation "de.dfki.mary:marytts-lang-en:5.2"
        implementation "de.dfki.mary:voice-cmu-slt-hsmm:5.2"
        implementation "de.dfki.mary:voice-cmu-rms-hsmm:5.2"
        implementation "de.dfki.mary:voice-cmu-bdl-hsmm:5.2"
        implementation "de.dfki.mary:voice-dfki-prudence-hsmm:5.2"
        implementation "de.dfki.mary:voice-dfki-poppy-hsmm:5.2"
        implementation "de.dfki.mary:voice-dfki-spike-hsmm:5.2"
        implementation "de.dfki.mary:voice-dfki-obadiah-hsmm:5.2"

        //implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        implementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    task assetsFullTest(type: Test) {
        workingDir "../android/assetsFull"

        // Enable JUnit 5 (Gradle 4.6+).
        useJUnitPlatform()

        // Always run tests, even when nothing changed.
        dependsOn 'cleanAssetsFullTest'

        // Show test results.
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

project(":android") {
    apply plugin: "android"
    apply plugin: "kotlin-android"

    configurations { natives }

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        implementation "com.google.android.gms:play-services-games:23.1.0"
        implementation "com.google.android.gms:play-services-auth:20.5.0"
        implementation "com.google.api-client:google-api-client-android:1.31.2"
        implementation "com.google.apis:google-api-services-drive:v3-rev197-1.25.0"
        implementation "com.google.http-client:google-http-client-gson:1.43.2"
    }
}

project(":ios") {
    apply plugin: "java"
    apply plugin: "robovm"
    apply plugin: "kotlin"

    dependencies {
        implementation project(":core")
        implementation "com.mobidevelop.robovm:robovm-rt:$roboVMVersion"
        implementation "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion"
        implementation "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-ios"
    }
}

project(":core") {
    apply plugin: "java"
    apply plugin: "kotlin"

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "com.squareup.okhttp3:okhttp:4.10.0"
        api "org.json:json:20230227"
        api "org.apache.commons:commons-lang3:3.11"
    }
}